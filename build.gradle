/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'fczheng' at '16-10-18 11:11:15' with Gradle 3.0
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.0/userguide/tutorial_java_projects.html
 */

apply plugin: 'java'
    
buildscript {
    ext {
        springBootVersion = '1.5.10.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    version = "0.0.1.SNAPSHOT"
}

subprojects {
    apply plugin: "org.springframework.boot"
    apply plugin: "idea"
    apply plugin: "eclipse"

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    def defaultEncoding = 'UTF-8'
    tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }

    javadoc {
        options.encoding = defaultEncoding
        options.addBooleanOption('Xdoclint:none', true)
    }

    test {
        include "**/controller/**", "**/service/**", "**/util/**"
    }

    repositories {
        jcenter()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
        }
    }

    dependencies {
        compile "org.springframework.boot:spring-boot-starter"
        compile "org.springframework.boot:spring-boot-starter-web"
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile "org.projectlombok:lombok:1.16.18"
        testCompile "org.springframework.boot:spring-boot-starter-test"
    }
}

project(':feign-common') {
    dependencies {
    }
    bootRepackage {
	    enabled = false
	}
}

project(':feign-provider') {
	dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-consul-dependencies:1.3.3.RELEASE'
        }
    }
    dependencies {
    	compile project(':feign-common')
    	compile("org.springframework.cloud:spring-cloud-starter-consul-discovery")
    }
}

project(':feign-consumer') {
    dependencyManagement {
        imports {
        	mavenBom 'org.springframework.cloud:spring-cloud-netflix:1.4.4.RELEASE'
        	mavenBom 'org.springframework.cloud:spring-cloud-consul-dependencies:1.3.3.RELEASE'
        }
    }
    dependencies {
    	compile project(':feign-common')
    	compile("org.springframework.cloud:spring-cloud-starter-feign")
    	compile("org.springframework.cloud:spring-cloud-starter-consul-discovery")
    	compile('org.springframework.cloud:spring-cloud-starter-netflix-hystrix')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.0"
}